prototype(Sitegeist.Monocle:InlineEditableTag) < prototype(TYPO3.TypoScript:Tag) {
	@context {
		propertyName = ${this.propertyName}
	}

	propertyName = NULL

	attributes = TYPO3.TypoScript:Attributes {

		class = TYPO3.TypoScript:RawArray {
			inlineEditableClass = 'neos-inline-editable'
			inlineEditableClass.@if.hasPropertyName = ${(node.context.workspaceName != 'live' && propertyName) ? TRUE : FALSE}
		}

		property = ${'typo3:' + propertyName }
		property.@if.hasPropertyName = ${(node.context.workspaceName != 'live' && propertyName) ? TRUE : FALSE}

		data-neos-node-type =  ${node != NULL ? q(node).property('_nodeType.name') : ''}
		data-neos-node-type.@if.hasNodeTypeName = ${((node != NULL) && (node.context.workspaceName != 'live')) ? TRUE : FALSE}
	}

	content = ''
	content.@process.hasPropertyName = ${propertyName ? q(node).property(propertyName) : value}

	@if.isNotLiveOrHasContent = ${node.context.workspaceName != 'live' || String.isBlank(this.content) == FALSE}
}